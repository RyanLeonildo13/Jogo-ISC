.data
.eqv MAX_INIMIGOS   8
.eqv TAM_INIMIGO    20   # bytes por inimigo (x, y, ativo, direcao, timer)
.eqv TAM_PX_INIMIGO 32   # largura/altura do inimigo em pixels

inimigos: .space 160     # MAX_INIMIGOS * TAM_INIMIGO

.text

# Colisão AABB: a0-a3 = x,y,largura,altura do objeto 1
#               a4-a7 = x,y,largura,altura do objeto 2
# retorna a0 = 1 se colidiu, 0 se não
check_collision:
    add t0, a0, a2
    add t2, a1, a3
    add t1, a4, a6
    add t3, a5, a7
    ble t0, a4, no_collision
    bge a0, t1, no_collision
    ble t2, a5, no_collision
    bge a1, t3, no_collision
    li a0, 1
    ret
no_collision:
    li a0, 0
    ret

# Checa colisão do mago com os inimigos ativos (hit-kill: 1 toque já basta)
checar_colisao_mago_inimigos:
    addi sp, sp, -32
    sw ra, 28(sp)
    sw s1, 24(sp)
    sw s2, 20(sp)
    sw s3, 16(sp)
    sw s4, 12(sp)
    sw s5, 8(sp)
    sw s6, 4(sp)
    sw s7, 0(sp)

    la t0, posicao_x_mago
    lw s1, 0(t0)
    la t0, posicao_y_mago
    lw s2, 0(t0)
    la t0, tamanho_mago
    lw s3, 0(t0)

    la s4, inimigos
    li s5, MAX_INIMIGOS
    li s6, TAM_INIMIGO
    li s7, TAM_PX_INIMIGO

colisao_loop:
    beqz s5, colisao_fim

    lw t0, 0(s4)
    lw t1, 4(s4)
    lw t2, 8(s4)
    beqz t2, colisao_next

    mv a0, s1
    mv a1, s2
    mv a2, s3
    mv a3, s3
    mv a4, t0
    mv a5, t1
    mv a6, s7
    mv a7, s7

    jal check_collision
    beqz a0, colisao_next

    jal tocar_som_goblin_ataca
    jal mago_atingido
    j colisao_fim

colisao_next:
    add s4, s4, s6
    addi s5, s5, -1
    j colisao_loop

colisao_fim:
    lw ra, 28(sp)
    lw s1, 24(sp)
    lw s2, 20(sp)
    lw s3, 16(sp)
    lw s4, 12(sp)
    lw s5, 8(sp)
    lw s6, 4(sp)
    lw s7, 0(sp)
    addi sp, sp, 32
    ret
